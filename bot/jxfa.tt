#nop 任务;
#class jxfa open;
#var f1_set 0;
#var f2_set 0;

#tic {jxfa} {#untic jxfa;jxfaa} {2};
#ali {jxfaa} {
  unset adv_quest;
  time;
  #var loc_lock 0;
  #var luck %1;
  #var bonus %2;
  #var enemies {};
  ask ji xiaofu about job;
};
#ali {jxfd} {say $code;jxfx};
#ali {jxff} {ask ji about fail;#untic jxfa1;jxfx;};
#ali {rj} {#var code %1};
#ali {cu} {
  clamber up;
  on_unbusy {wield $wp}
};
#ali {pa} {clamber up};
#ali {ld} {look down};
#ali {jd} {
  #if {"$myid"=="dalabomba"} {wi $lun;yun longxiang};
  #elseif {"$myid"=="simontuixue"} {p.yy};
  #elseif {"$myid"=="jingirl"} {yun powerup};
  leap down;
};

#act {^纪晓芙在你耳边轻声说道：$myid 据说他们会在%*密谋。} {
  #if {$loc_lock==0} {
    #var loc_lock 1;
    #var loc %1;
    #tic {jxfa1} {#echo {%c%h} {light yellow} {同伙在%1，到了clamber up。}} {5};
  };
};
#action {^你刚刚把自己藏起来，立刻听到了脚步声。} {
  #var code {};
  #class conv open;
  #action {^%%!s│%%!s%%*%%!s＞%%!*$} {
    #nop 左侧首行数据;
      process %%1;
  };
  #action {^%%!s│%%!s%%*%%!s│%%!*$} {
    #nop 左侧续行数据;
      process %%1;
  };
  #action {^%%!*＜%%!s%%*%%!s│%%!s$} {
    #nop 右侧首行数据;
      process %%1;
  };
  #action {^%%!*│%%!s%%*%%!s│%%!s$} {
    #nop 右侧续行数据;
      process %%1;
  };
  #action {^[%%*]%%*和%%*转身离去。$} {
    #showme <afa> $code;
    #class conv kill;
    jd;
  };
  #class conv close;
};
#alias process {
  #if {@ismember{%1;blacklist} == 0} {
    #var code ${code}%1;
    #showme <afa> 未命中黑名单，记录: $code;
  };
  #else {#showme <afa> 命中黑名单，丢弃}
};

#alias gg {
  get all from $enemies[1];
  on_unbusy {
    get all from $enemies[2];
  }
};
#act {^%!s「%!*」%*(%*) <战斗中>$} {
  #var oppo @lower{%2};
  #if {@ismember{$oppo;enemies}==0} {#list enemies add $oppo};
};

#list blacklist create {
  首领布置了天罗地网对;
  付姓杨的，谅他这次插;
  翅难逃。;
  还用你说？;
  那还用说？;
  这毫无疑问。;
  我也是这样想的。;
  我们说啥来对口令的？;
  行动的口令我记不太清;
  了……;
  真要我再说一遍？;
  你怎么这都记不住？;
  暗号是啥来着的？;
  附耳过来！;
  真笨！;
  嘿嘿嘿嘿;
  哈哈哈哈;
  哦;
  我最近偶感风寒，什么;
  都记不住;
  真是这个？
};

#act {看起来%1想杀死你} {#var jxfss %1};

#alias gc2 {get all from corpse 1;get all from corpse 2;get all from $enemies[1]; get all from $enemies[2]};
#gag {^[舜耕山]%*$};
#class jxfa close;
