#nop 任务;
#class hsza open;
#var f1_set 0;
#var f2_set 0;

#tic {hsza} {#untic hsza;hszaa} {2};
#ali {hszaa} {
  #format time1 {%t} {%Y-%m-%d-%H:%M:%S};
  #var loc_lock 0;
  #var luck %1;
  #var bonus %2;
  #var i_sk 1;
  set wimpy 70;
  set hansz_new_mode 1;
  ask han about job;
  check_bonus;
};
#ali {hszd} {sm;ask han about finish;};
#ali {hszf} {ask han about fail;#untic hsza1;hszx;};
#ali {findroom} {
  #class my_gps read bot/my_gps.tt;
  locme $city %1;
};

#act {^韩世忠说道：「我听说有一群异族奸细在%*的%*附近出没%*去打探(datan)一下%*} {

  #if {$loc_lock==0} {
    #var loc_lock 1;
    #var city %1;
    #var room %2;
    #var loc $city的$room;
    #tic {hsza1} {#echo {%c%h} {light yellow} {奸细在$loc，到了datan。}} {5};
  };
};
#act {^你紧紧捏住手中的韩王兵符，令亲兵留在原地，自己单刀赴会。} {
  #delay 3 {
    set adv_quest 50;
    special agile;
    #untic hsza1;
    wear mine;
    unwield all;
    #if {"$myid"=="dalabomba"} {
      special ironskin;
      wield $lun;
    };
    #if {"$myid"=="simontuixue"} {
      yun shield;
      remove armet;
      remove belt;
      wi shield;
      uw $wp;
      p.yy
    };
    #if {"$myid"=="jingirl"} {
      uw all;
      wield $wp;
      special ironskin;
      wear zhiyan;wear zhixin;
    };
    #if {"$myid"=="jinger"} {
      wield $wp;
    }
  }
};
#alias peace {
  #var battle 0;
  #var VERBOSE 1;
  unset brief;
};
#act {快去找韩王复命吧。} {
  #delay 4 {
    peace;
  }
};

#act {%*异族奸细 %*(%*)%*} {
  #if {$f1_set==0}{
    #var oppo @lower{%3};
    zhuanzhu $oppo;
    #var f1_set 1;
  };
  #elseif {$f2_set==0}{
    #var oppo_bm @lower{%3};
    #var oppo_dq @lower{%3};
    #var f2_set 1;
  };
};

#alias bm {
  yun beiming $oppo_bm;
};

#alias gm {
  get gem from corpse %1;
};

#act {看起来%*想杀死你} { 
  #if {$battle != 1} {
    #var battle 1;
    #if {"$myid"=="dalabomba"} {
      yun longxiang;
    };
    #elseif {"$myid"=="simontuixue"} {
      #delay 2 {bm;}
    };
    #else {yun powerup}
  };
};

#nop #act {}{};
#action {你在攻击中不断积蓄攻势。(气势：%d%)} {
  #var qi %1;
  #if {"$myid"=="simontuixue" && $qi >= $qishi[$i_sk]} {
    p.$skills[$i_sk] $oppo;
  };
  #elseif {"$myid"=="jinger" && $qi >= $qishi[$i_sk]} {
    p.$skills[$i_sk] $oppo;
  };
  #elseif {"$myid"=="jingirl" && $qi >= $qishi[$i_sk]} {
    p.$skills[$i_sk];
  };
  #elseif {"$myid"=="dalabomba" && $qi >= $qishi[$i_sk]} {
    p.$skills[$i_sk];
  }
};
#act {%*的作用时间还没过，请过一段时间再施展绝技。} {
  #math i_sk {$i_sk+1};
  #if {$i_sk>=$n_sk} {
    #var i_sk 1;
  };
  #if {$qi >= $qishi[$i_sk]} {
    p.$skills[$i_sk];
  }
};
#act {^眼前敌人尽去，你顺势圈转手中%*，轻轻吐出胸中浊气，心头大是畅快！} {
  #var i_sk 1;
};
#act {^%*你终于完全从紧张地战斗氛围中解脱出来。} {
  #var battle 0;
  unset brief;
  #var VERBOSE 1;
};
#act {%*死了。$} {
  #var f1_set 0;
  #if {"$myid"=="simontuixue"} {
    l;
    #delay 2 {bm;}
  }
};
#alias f1 {
  #var oppo {%1};
  #var f1_set 1;
};
#alias f2 {
  #var oppo_bm {%1};
  #var oppo_dq {%1};
  #var f2_set 1;
};
#alias tx {
  kill all;
  #if {"$myid"=="simontuixue"} {
    p.lm $oppo;
  };
  #elseif {"$myid"=="dalabomba"} {
    p.dq $oppo_dq;
  };
  #elseif {"$myid"=="jingirl"} {
    p.pf;
  }
};

#alias jl {
  #var n_oppo %0;
  #showme <afa> OK, n_oppo has been set to $n_oppo;
};
#action {^快去找韩王复命吧。} {
  unset adv_quest;
  wear mine;
  remove belt;
  remove armet;
  uw all;
  wi $wp;
};

#alias hszLog {
  #line log {hsz.log} {%0};
};
#act {你瞬间感到了危险临近} {
  unset adv_quest;
};

#alias jm {
  #var job_mod %0;
}
#act {^{┌|q}───%*{之周|同庆}{┬─|}─────────────────────────────────{r|┐}$} {
  #class week open;
    #act {^│%s韩世忠%s│%*。%s│$} {#var job_mod 2};
    #act {^│%*│→%*韩世忠%*%s│$} {#var job_mod  0};
    #act {^│%*│↑%*韩世忠%*%s│$} {#var job_mod  1};
    #act {^│%*│↓%*韩世忠%*%s│$} {#var job_mod -1};
    #act {^{t|└}───────┴─────────────────────────北大侠客行────{s|┘}$} {#class week kill};
  #class week close;
  #delay 1 {#showme <afa> bonus_mode: $job_mod;}
};
#class report open;
  #act {^%!s%*点经验；$} {
    #var exp %1;
  };
  #act {^%!s%*点潜能；$} {
    #var pot %1;
  };
  #act {^%!s%*点江湖声望。$} {
    #var fam %1;
  };
  #act {^你已经完成了%*次清除异族奸细的工作。$} {
    #var taskid %1;
    #class report kill;
    hszLog $record;
    #showme <afa> $record;
    #untic hsza1;hszx;
  };
#class report close;

#act {^│幸运值%!s│%d%!s│奖励系数%!s│%d% %!s│识图检查%!s│%*完整奖励%*│$} { #var luck %1; #var bonus %2; };
#act {你被奖励了：} {
  #format time2 {%t} {%H:%M:%S};
  #format record {%s;%s;%s;%d;%d;%d;%s;%s;%s;%s;%s} {$job_mod} {$loc} {$n_oppo} {@ctd{$exp}} {@ctd{$pot}} {@ctd{$fam}} {@ctd{$taskid}} {$time1} {$time2} {$luck} {$bonus};
};
#act {你已经陷入半昏迷状态，随时都可能摔倒晕去。} {
  #if {"$myid"=="jingirl"} {xq}
};

#act {你被%*「斗转星移」功夫所引导，招招击向自己！} {
  yield yes;
};
#gag {^>%s$};
#gag {^$};

#gag {^你设定每一击用};
#gag {^你已在运用北冥神功};
#class hsza close;
